//----------------------------------------------------------------------------------------------
// Autor ....: Alexandro Maule - Harsy Sistemas
// Criado em : 25/04/2016
// Vers∆o 0.0.1
//----------------------------------------------------------------------------------------------
// Parametros:
// 1 - Array com a lista dos xmls a serem carregados.
// 2 - CNPJ do emitente.
//----------------------------------------------------------------------------------------------
// Retorno:
// Retorna uma array com os dados das NFe's a serem gerados.
//----------------------------------------------------------------------------------------------
// Observaá‰es:
// As xmls das NFe's que n∆o estiverem autorizadas, ser∆o descartadas sem nenhum aviso de erro.
//----------------------------------------------------------------------------------------------

Use Pacotes\cWSGeraGuiaGnre.pkg
Use Flexml.pkg

Function GGGLerXmls Global String[] saListaXmls String sCNPJ Returns tWStListaNfe
    tWStListaNfe NFEs       // Dados para retorno
    Integer iContador       // Auxiliar para loop
    Integer iQtd_nfes       // Auxiliar para posicionar
    Handle  hoXml           // Objeto xml
    Handle  hoNFe           // Receber† o Id do elemento NFe
    Handle  hoprotNFe       // Receber† o Id do elemento protNFe
    Handle  hoinfProt       // Receber† o Id do elemento infProt
    Handle  hoRoot          // Receber† o Id do elemento root
    Handle  hoInfNFe        // Receber† o Id do elemento infNFe
    Handle  hoTotal         // Receber† o Id do elemento total
    Handle  hoICMSTot       // Receber† o Id do elemento ICMStot
    Handle  hoDest          // Receber† o Id do elemento Dest
    Handle  hoEnderDest     // Receber† o Id do elemento EnderDest
    Handle  hoide           // Receber† o Id do elemento ide
    Handle  hoAux           // Handle tempor†rio
    Boolean bOk             
    String  sAux            // Nome do xml
    String  sValor          // Recebe valores USA e converte para BR
    
    Boolean existe_arquivo
    
    For iContador from 0 to ((SizeOfArray(saListaXmls)) - 1)
        File_Exist saListaXmls[iContador] existe_arquivo
        If existe_arquivo ;
        Begin
            Move saListaXmls[iContador] to sAux
            Get Create U_cXMLDOMDocument to hoXml
            If not hoXml Function_Return NFEs                        

//            Set pbPreserveWhiteSpace of hoXML to False
            Set psDocumentName of hoXML to sAux
            Get LoadXMLDocument of hoXML to bOk

            If bok;
            Begin            
            // Obtendo elemento root 
                Get DocumentElement of hoXML to hoRoot
                Get ChildElementNS of hoRoot "http://www.portalfiscal.inf.br/nfe" "NFe" to hoNFe
                Get ChildElementNS of hoRoot "http://www.portalfiscal.inf.br/nfe" "protNFe" to hoprotNFe
                If (hoNFe and hoprotNFe) ;
                Begin
                    Get ChildElementNS of hoNFe "http://www.portalfiscal.inf.br/nfe" "infNFe" to hoInfNFe
                    If hoInfNFe ;
                    Begin
                        Get ChildElementNS of hoInfNFe "http://www.portalfiscal.inf.br/nfe" "dest" to hoDest
                        If hoDest ;
                        Begin
                            Get ChildElementNS of hoDest "http://www.portalfiscal.inf.br/nfe" "CNPJ" to hoAux
                            If hoAux ;
                            Begin
                                Get psText of hoaux to NFEs.itens[iQtd_nfes].cnpj
                                Send Destroy of hoAux
                            End
                            Else ;
                            Begin
                                Get ChildElementNS of hoDest "http://www.portalfiscal.inf.br/nfe" "CPF" to hoAux
                                Get psText of hoaux to NFEs.itens[iQtd_nfes].cpf
                                Send Destroy of hoAux
                            End
                            
                            Move 0 to hoAux
                            Get ChildElementNS of hoDest "http://www.portalfiscal.inf.br/nfe" "xNome" to hoAux
                            Get psText of hoaux to NFEs.itens[iQtd_nfes].nome_destinatario
                            Send Destroy of hoAux

                            
                            Get ChildElementNS of hoDest "http://www.portalfiscal.inf.br/nfe" "enderDest" to hoEnderDest
                            
                            If hoEnderDest ;
                            Begin
                                Get ChildElementNS of hoEnderDest "http://www.portalfiscal.inf.br/nfe" "cMun" to hoAux
                                Get psText of hoaux to NFEs.itens[iQtd_nfes].cod_municipio_destinat
                                Send Destroy of hoAux
                                
                                Get ChildElementNS of hoEnderDest "http://www.portalfiscal.inf.br/nfe" "UF" to hoAux
                                Get psText of hoaux to NFEs.itens[iQtd_nfes].uf_destinatario
                                Send Destroy of hoAux
                                Send Destroy of hoEnderDest

                            End

                            Get ChildElementNS of hoprotNFe "http://www.portalfiscal.inf.br/nfe" "infProt" to hoinfProt
                            If hoinfProt ;
                            Begin
                                Get ChildElementNS of hoinfProt "http://www.portalfiscal.inf.br/nfe" "chNFe" to hoAux
                                Get psText of hoaux to NFEs.itens[iQtd_nfes].chave_nfe
                                Send Destroy of hoAux
                                Send Destroy of hoinfProt
                            End
                            
                            Get ChildElementNS of hoInfNFe "http://www.portalfiscal.inf.br/nfe" "ide" to hoide

                            Get ChildElementNS of hoide "http://www.portalfiscal.inf.br/nfe" "nNF" to hoAux
                            Get psText of hoaux to NFEs.itens[iQtd_nfes].num_nota_fiscal
                            Send Destroy of hoAux

                            Get ChildElementNS of hoide "http://www.portalfiscal.inf.br/nfe" "dhEmi" to hoAux
                            Get psText of hoaux to NFEs.itens[iQtd_nfes].data_emissao
                            Send Destroy of hoAux

                            Get ChildElementNS of hoide "http://www.portalfiscal.inf.br/nfe" "mod" to hoAux
                            Get psText of hoaux to NFEs.itens[iQtd_nfes].modelo
                            Send Destroy of hoAux

                            Get ChildElementNS of hoide "http://www.portalfiscal.inf.br/nfe" "serie" to hoAux
                            Get psText of hoaux to NFEs.itens[iQtd_nfes].serie
                            If ((Length(Trim(NFEs.itens[iQtd_nfes].serie))) < 3) Move (Insert('0',NFEs.itens[iQtd_nfes].serie,1)) to NFEs.itens[iQtd_nfes].serie
                            If ((Length(Trim(NFEs.itens[iQtd_nfes].serie))) < 3) Move (Insert('0',NFEs.itens[iQtd_nfes].serie,1)) to NFEs.itens[iQtd_nfes].serie
                            Send Destroy of hoAux
                            Send Destroy of hoide

                            Move 10 to NFEs.itens[iQtd_nfes].tipo_documento // atualmente Ç fixo, indica nota fiscal

                            Get ChildElementNS of hoInfNFe "http://www.portalfiscal.inf.br/nfe" "total" to hoTotal
                            Get ChildElementNS of hoTotal "http://www.portalfiscal.inf.br/nfe" "ICMSTot" to hoICMSTot

                            Get ChildElementNS of hoICMSTot "http://www.portalfiscal.inf.br/nfe" "vICMSUFDest" to hoAux
                            If hoAux ;
                            Begin
                                Get psText of hoaux to sValor
                                Move (Replace('.',sValor,',')) to NFEs.itens[iQtd_nfes].icms_uf_destino
                                Send Destroy of hoAux
                            End

                            Get ChildElementNS of hoICMSTot "http://www.portalfiscal.inf.br/nfe" "vFCPUFDest" to hoAux
                            If hoAux ;
                            Begin
                                Get psText of hoaux to sValor
                                Move (Replace('.',sValor,',')) to NFEs.itens[iQtd_nfes].fcp_uf_destino
                                Send Destroy of hoAux
                            End

                            Get ChildElementNS of hoICMSTot "http://www.portalfiscal.inf.br/nfe" "vST" to hoAux
                            If hoAux ;
                            Begin
                                Get psText of hoaux to sValor
                                Move (Replace('.',sValor,',')) to NFEs.itens[iQtd_nfes].icms_st
                                Send Destroy of hoAux
                            End
                            Move NFEs.itens[iQtd_nfes].data_emissao to NFEs.itens[iQtd_nfes].vencimento
                            Move sCNPJ to NFEs.itens[iQtd_nfes].emitente
                            
                            Increment iQtd_nfes

                            Send Destroy of hoICMSTot
                            Send Destroy of hoTotal
                            Send Destroy of hoDest
                            Send destroy of hoInfNFe
                            Send destroy of hoprotNFe
                            Send destroy of hoRoot
                            Send destroy of hoNFe
                        End
                    End
                End
            End
            Send Destroy of hoXml
        End
    Loop

    Function_Return NFEs

End_Function